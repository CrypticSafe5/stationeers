# Constants
define pressureMax 52000 # kPa
define tempMax 1500 # K

# Devices
alias pressureDial d0
alias pressureRegIn d1
alias pressureRegOut d2
alias tempDial d3
alias tempReg d4
alias personSensor d5

jal initialize

main:
jal setPressure
jal setTemp
jal setLights
j main

initialize:
s pressureRegIn On 1
s pressureRegIn Lock 1
s pressureRegOut On 1
s pressureRegOut Lock 1
s tempReg On 0
s tempReg Lock 1
s tempReg Mode 1
j ra

setLights:
# If person is detected, turn on lights
l r0 personSensor Activate
push ra
bgtal r0 0 setLightsOn
beqal r0 0 setLightsOff
pop ra
j ra

setLightsOn:
sb HASH("StructureWallLight") On 1
j ra

setLightsOff:
sb HASH("StructureWallLight") On 0
j ra

setPressure:
# Check that reg is not above max
# If value is not dial value, set
l r0 pressureDial Setting
push ra
bltal r0 pressureMax setTargetPressure
beqal r0 pressureMax setTargetPressure
bgtal r0 pressureMax setMaxPressure
pop ra
j ra

setTargetPressure:
s pressureRegIn Setting r0
s pressureRegOut Setting r0
j ra

setMaxPressure:
s pressureRegIn Setting pressureMax
s pressureRegOut Setting pressureMax
j ra

setTemp:
# Check that temp is not above max
# If value is not dial value, set
l r0 tempDial Setting
push ra
bltal r0 tempMax setTargetTemp
beqal r0 tempMax setTargetTemp
bgtal r0 tempMax setMaxTemp
pop ra
j ra

setTargetTemp:
s tempReg Setting r0
j ra

setMaxTemp:
s tempReg Setting tempMax
j ra